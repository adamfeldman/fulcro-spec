= Untangled Spec Docs
:source-highlighter: coderay
:source-language: clojure
:toc:
:toc-placement!:
:toclevels: 3
:sectlinks:
:sectanchors:
:sectnums:

ifdef::env-github[]
:tip-caption: :bulb:
:note-caption: :information_source:
:important-caption: :heavy_exclamation_mark:
:caution-caption: :fire:
:warning-caption: :warning:
endif::[]

ifdef::env-github[]
toc::[]
endif::[]

== Features

The macros in untangled-spec wrap clojure/cljs test, so that you may use any of the features of the core library.
The specification DSL makes it much easier to read the tests, and also includes a number of useful features:

- Outline rendering
- Left-to-right assertions
- More readable output, such as data structure comparisons on failure (with diff notation as well)
- Real-time refresh of tests on save (client and server)
- Seeing test results in any number of browsers at once
- Mocking of normal functions, including native javascript (but as expected: not macros or inline functions)
    - Mocking verifies call sequence and call count
    - Mocks can easily verify arguments received
    - Mocks can simulate timelines for CSP logic
- Protocol testing support (helps prove network interactions are correct without running the full stack)

== Setting up

Please use the Untangled Tutorial or TodoMVC projects as samples for setting up a project.

As an example, we can look inside untangled-spec for the following files:

- link:../test/untangled_spec/all_tests.cljs[all_tests.cljs] : An entry point for CI testing from the command line.
- link:../dev/cljs/user.cljs[user.cljs] : The entry point for browser test rendering.
- link:../test/untangled_spec/tests_to_run.cljs[tests_to_run.cljs] : A file that does nothing more than require all of the specs. The test runners search for
testing namespaces, so if you don't load them somewhere, they won't be found. Since there are two places tests
run from (browser and CI) it makes sense to make this DRY.
- `*_spec.clj(s)` : Are spec files containing tests/assertions.

There is a top level link:../package.json[package.json] file for installing node packages to run CI tests.

The link:../project.clj[project.clj] includes various things to make all of this work:

- The https://github.com/bensu/doo#usage[lein doo plugin], for running tests through karma *via* node (in Chrome).
- A `:doo` section to configure the CI runner
- A cljsbuild for testing with figwheel true. The build with id "test" link:../project.clj#L36[here], is the browser test build.
- A cljsbuild for testing with lein doo. The build with id "automated-tests" link:../project.clj#L47[here], is the CI tests output.
- The https://github.com/jakemcc/lein-test-refresh[lein test-refresh plugin], which will re-run server tests on save, and also can be configured with the spec renderer (see the `:test-refresh` section in the project file).

== Running server tests

To run all specs, just use `lein test-refresh` at the command line, +
and make sure https://github.com/jakemcc/lein-test-refresh[jakemcc/lein-test-refresh] is in your `:plugins`.

== Running client tests (during development)

Just include `-Dtest` in your JVM argument list, or run `(start-figwheel ["test"])` in the server user.clj file.
This will cause the test build to start running via figwheel. Then just open the http://localhost:3449/test.html file in your browser.

=== Running CI browser tests

See https://github.com/bensu/doo#usage[lein doo] for details, but basically run `lein doo {js-env} automated-tests once`.

== Anatomy of a specification

The main testing macros are `specification`, `behavior`, and `assertions`:

```
(specification "A Thing"
   (behavior "does something"
      (assertions
         form => expected-form
         form2 => expected-form2

         "optional sub behavior clause"
         form3 => expected-form3)))
```

The specification macro just outputs a `(clojure|cljs).test/deftest`, so you are free to use `is`, `are`, `do-report`, etc.

The `behavior` macro outputs additional events for the renderer to make an outline. As a note, `component` is just a macro alias of `behavior` it just can read better if what you are describing is not a behavior but a *component* of some greater thing.

=== Assertions

Assertions provides some explict arrows, unlike https://github.com/marick/Midje[Midje] which uses black magic, for use in making your tests more concise and readable.

[source]
----
(assertions
  actual => expected ;;<1>
  actual =fn=> expected ;;<2>
  actual =throws=> (ExceptionType opt-regex opt-pred)) ;;<3><4><5>
----
<1> Checks that actual is equal to expected, either can be anything.
<2> `expected` is a function takes `actual` and returns a truthy value.
<3> Expects that actual will throw an Exception and checks that the type is `ExceptionType`.
<4> Optionally that the message matches the `opt-regex`.
<5> Optionally matches that it passes the `opt-pred`.

=== Mocking

The mocking system does a lot in a very small space. It can be invoked via the `provided` or `when-mocking` macro.
The former requires a string and adds an outline section. The latter does not change the outline output.

Mocking must be done in the context of a specification, and creates a scope for all sub-outlines. Generally
you want to isolate mocking to a specific behavior:

```
;; source file
(defn my-function [x y] (launch-rockets!))
;; spec file
(specification "Thing"
  (behavior "Does something"
    (when-mocking
      (my-function arg1 arg2)
      => (do (assertions
               arg1 => 3
               arg2 => 5)
           true)
      ;;actual test
      (assertions
        (my-function 3 5) => true))))
```

Basically, you include triples (a form, arrow, form), followed by the code & tests to execute.

It is important to note that the mocking support does a bunch of verification at the end of your test:

- It verifies that your functions are called the appropriate number of times (at least once is the default)
- It uses the mocked functions in the order specified.
- It captures the arguments in the symbols you provide (in this case arg1 and arg2). These
are available for use in the RHS of the mock expression.
- It returns whatever the RHS of the mock expression indicates
- If assertions run in the RHS form, they will be honored (for test failures)

So, the following mock script:

```
(when-mocking
   (f a) =1x=> a
   (f a) =2x=> (+ 1 a)
   (g a b) => 17

   (assertions
     (+ (f 2) (f 2) (f 2)
        (g 3e6 :foo/bar) (g "otherwise" :invalid)
     => 42))
```

should pass. The first call to `f` returns the argument. The next two calls return the argument plus one.
`g` can be called any amount (but at least once) and returns 17 each time.

If you were to remove any call to `f` or `g` this test would fail.

==== Spies

Sometimes it is desirable to check that a function is called but still use its original definition, this pattern is called a test spy.
Here's an example of how to do that with untangled spec:

[source]
----
(let [real-fn f]
  (when-mocking f => (do ... (real-fn))
  (assertions
    ...)
----

==== Protocols and Inline functions

When working with protocols and records, or inline functions (eg: https://github.com/clojure/clojure/blob/clojure-1.8.0/src/clj/clojure/core.clj#L965[+]), it is useful to be able to mock them just as a regular function.
The fix for doing so is quite straightforward:
[source]
----
;; source file
(defprotocol MockMe
  (-please [this f x] ...)) ;;<1>
(defn please [this f x] (-please this f x)) ;;<2>

(defn fn-under-test [this]
  ... (please this inc :counter) ...) ;;<3>

;; test file
(when-mocking
  (please this f x) => (do ...) ;;<4>
  (assertions
    (fn-under-test ...) => ...))) ;;<5>
----
<1> define the protocol & method
<2> define a function that just calls the protocol
<3> use the wrapper function instead of the protocol
<4> mock the wrapping function from (2)
<5> keep calm and carry on testing

=== Timeline testing

On occasion you'd like to mock things that use callbacks. Chains of callbacks can be a challenge to test, especially
when you're trying to simulate timing issues.

```
(def a (atom 0))

(specification "Some Thing"
  (with-timeline
    (provided "things happen in order"
              (js/setTimeout f tm) =2x=> (async tm (f))

              (js/setTimeout
                (fn []
                  (reset! a 1)
                  (js/setTimeout
                    (fn [] (reset! a 2)) 200)) 100)

              (tick 100)
              (is (= 1 @a))

              (tick 100)
              (is (= 1 @a))

              (tick 100)
              (is (= 2 @a))))
```

In the above scripted test the `provided` (when-mocking with a label) is used to mock out `js/setTimeout`. By
wrapping that provided in a `with-timeline` we gain the ability to use the `async` and `tick` macros (which must be
pulled in as macros in the namespace). The former can be used on the RHS of a mock to indicate that the actual
behavior should happen some number of milliseconds in the *simulated* future.

So, this test says that when `setTimeout` is called we should simulate waiting however long that
call requested, then we should run the captured function. Note that the `async` macro doesn't take a symbol to
run, it instead wants you to supply a full form to run (so you can add in arguments, etc).

Next this test does a nested `setTimeout`! This is perfectly fine. Calling the `tick` function advances the
simulated clock. So, you can see we can watch the atom change over \"time\"!

Note that you can schedule multiple things, and still return a value from the mock!

```
(with-timeline
  (when-mocking
     (f a) => (do (async 200 (g)) (async 300 (h)) true)))
```

the above indicates that when `f` is called it will schedule `(g)` to run 200ms from \"now\" and `(h)` to run
300ms from \"now\". Then `f` will return `true`.

== Other things of interest

Untangled spec also has:

- `component`: Identical to behavior, but is useful for making specs more readable (creates a sub-section of outline for a sub-area)
- `provided`: Similar to `when-mocking`, but requires a string, which is added as a subsection of the outline. The idea
with this is that your mocking is stating an assumption about some way other parts of the system are behaving for that test.
